## Class for settings of variables for building c++ on different archs
##
## This class sets the variables needed for when compiling c++ on native,
## machine, sdk, cross, sdk-cross, canadian-cross (=all).
## A similar class exists for c, see the c class.

inherit c

OVERRIDES .= ":cxx"

CLASS_DEPENDS += "${CXX_DEPENDS}"
CXX_DEPENDS			= "${CXX_DEPENDS_HOST}"
CXX_DEPENDS:>cross		= " ${CXX_DEPENDS_TARGET}"
CXX_DEPENDS:>sdk-cross		= " ${CXX_DEPENDS_TARGET}"
CXX_DEPENDS:>canadian-cross	= " ${CXX_DEPENDS_TARGET}"
CXX_DEPENDS_HOST		= "host-cross:c++ host:libstdc++"
CXX_DEPENDS_TARGET		= "target-cross:c++ target:libstdc++"

export CXX
export CXXFLAGS
export BUILD_CXX
export BUILD_CXXFLAGS

BUILD_CXX	= "${BUILD_PREFIX}g++"
HOST_CXX	= "${HOST_PREFIX}g++"
TARGET_CXX	= "${TARGET_PREFIX}g++"
CXX		= "${HOST_CXX}"

export CXXCPP = "${HOST_CXXCPP}"
export BUILD_CXXCPP
BUILD_CXXCPP	= "${BUILD_CXX} -E"
HOST_CXXCPP	= "${HOST_CXX} -E"
TARGET_CXXCPP	= "${TARGET_CXX} -E"

BUILD_CXXFLAGS		?= "${BUILD_CFLAGS} -fpermissive"
MACHINE_CXXFLAGS	?= "${MACHINE_CFLAGS} -fpermissive"
SDK_CXXFLAGS		?= "${SDK_CFLAGS} -fpermissive"
CXXFLAGS			 = "${HOST_CXXFLAGS}"
HOST_CXXFLAGS:native		 = "${BUILD_CXXFLAGS}"
HOST_CXXFLAGS:cross		 = "${BUILD_CXXFLAGS}"
HOST_CXXFLAGS:machine		 = "${MACHINE_CXXFLAGS} ${HOST_PIC_OPTION}"
HOST_CXXFLAGS:sdk		 = "${SDK_CXXFLAGS}"
HOST_CXXFLAGS:sdk-cross		 = "${BUILD_CXXFLAGS}"
HOST_CXXFLAGS:canadian-cross	 = "${SDK_CXXFLAGS}"
TARGET_CXXFLAGS:native		 = "${BUILD_CXXFLAGS}"
TARGET_CXXFLAGS:cross		 = "${MACHINE_CXXFLAGS}"
TARGET_CXXFLAGS:machine		 = "${MACHINE_CXXFLAGS} ${TARGET_PIC_OPTION}"
TARGET_CXXFLAGS:sdk		 = "${SDK_CXXFLAGS}"
TARGET_CXXFLAGS:sdk-cross	 = "${SDK_CXXFLAGS}"
TARGET_CXXFLAGS:canadian-cross	 = "${MACHINE_CXXFLAGS}"

# Local Variables:
# mode: python
# End:
